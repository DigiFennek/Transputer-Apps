VAL K IS 1024 :
VAL COUNT IS 28 :
[COUNT]NODE transputer :
ARC hostlink :

NETWORK
  DO
    DO i = 0 FOR 4
      SET transputer[i] (type, memsize := "T800", 4096*K)

    DO i = 4 FOR 10
      SET transputer[i] (type, memsize := "T800", 1024*K)

    DO i = 14 FOR 14
      SET transputer[i] (type, memsize := "T800", 128*K)

    CONNECT transputer[0][link][0] TO HOST WITH hostlink

    DO i = 0 FOR COUNT - 1
      CONNECT transputer[i][link][2] TO transputer[i + 1][link][1]

    DO i = 0 FOR COUNT - 4
      CONNECT transputer[i][link][3] TO transputer[i + 4][link][0]
:

#INCLUDE "hostio.inc"
#INCLUDE "mandel.inc"

#USE "master.c8h"
#USE "worker.c8h"

CONFIG
  CHAN OF SP fs, ts :
  PLACE fs, ts ON hostlink :

  [COUNT]CHAN OF PIPE to.worker, from.worker :
  CHAN OF PIPE loopback :

  PAR
    PROCESSOR transputer[0]
      master (fs, ts, to.worker[1], from.worker[1], COUNT - 1)

    PAR i = 1 FOR COUNT - 2
      PROCESSOR transputer[i]
        worker (to.worker[i], to.worker[i + 1], from.worker[i + 1], from.worker[i])

    PROCESSOR transputer[COUNT - 1]
      worker (to.worker[COUNT - 1], loopback, loopback, from.worker[COUNT - 1])
:
